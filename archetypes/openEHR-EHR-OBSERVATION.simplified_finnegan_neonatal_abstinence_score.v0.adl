archetype (adl_version=1.4; uid=e98e0f74-96eb-464f-8fb7-fd96a68da99e)
	openEHR-EHR-OBSERVATION.simplified_finnegan_neonatal_abstinence_score.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>
	translations = <
		["es-ar"] = <
			language = <[ISO_639-1::es-ar]>
			author = <
			>
		>
	>

description
	original_author = <
		["date"] = <"2022-02-23">
		["name"] = <"Maxine G. Harjani">
		["email"] = <"maxinegh@gmail.com">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Rosario Silva, rosario.silva.s@outlook.com", ...>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the results for each component parameter for the Simplified Finnegan Neonatal Abstinence Score (sFNAS).">
			keywords = <"neonatal, maternal opioid abuse, neonatal abstinence syndrome", ...>
			copyright = <"@Cambio CDS">
			use = <"Use to record the results for each component parameter for the Simplified Finnegan Neonatal Abstinence Score (sFNAS). 

The sFNAS is meant to be used for both current status and change in status over time.">
			misuse = <"Not to be used to record information about symptoms related to abstinence syndrome. Use instead openEHR-EHR-CLUSTER.symptom_sign.v1. 

Not to be confused with other neonatal abstinence or withdrawal assessments or other versions of the Finnegan Neonatal Abstinence Score such as the Modified Finnegan Neonatal Abstinence Score. Use instead the specific archetype, openEHR-EHR-OBSERVATION.modified_finnegan_neonatal_abstinence_score_nas.v0. ">
		>
		["es-ar"] = <
			language = <[ISO_639-1::es-ar]>
			copyright = <"© openEHR Foundation">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Gomez Pomar E, Finnegan LP, Devlin L, Bada H, Concina VA, Ibonia KT, Westgate PM. Simplification of the Finnegan Neonatal Abstinence Scoring System: retrospective study of two institutions in the USA. BMJ Open. 2017 Sep 27;7(9):e016176. doi: 10.1136/bmjopen-2017-016176. PMID: 28963285; PMCID: PMC5623549. ">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"1b255d66e8aedf86421e7b482ffd94ce">
		["build_uid"] = <"50a7ff5a-745e-36d1-b91d-0fa62c7a5b0b">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
	>

definition
	OBSERVATION[at0000] matches {    -- Simplified Finnegan Neonatal Abstinence Score
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..*} matches {    -- Any event
						data matches {
							ITEM_TREE[at0003] matches {    -- Tree
								items cardinality matches {0..*; unordered} matches {
									CLUSTER[at0039] occurrences matches {0..1} matches {    -- Central nervous system signs
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0045] occurrences matches {0..1} matches {    -- Cry
												value matches {
													0|[local::at0046],
													2|[local::at0047]
												}
											}
											ELEMENT[at0050] occurrences matches {0..1} matches {    -- Tremors
												value matches {
													0|[local::at0051],
													1|[local::at0052],
													5|[local::at0053]
												}
											}
											ELEMENT[at0054] occurrences matches {0..1} matches {    -- Increased muscle tone
												value matches {
													0|[local::at0055],
													2|[local::at0056]
												}
											}
											ELEMENT[at0057] occurrences matches {0..1} matches {    -- Sleep
												value matches {
													0|[local::at0046],
													1|[local::at0059],
													3|[local::at0060]
												}
											}
										}
									}
									CLUSTER[at0042] occurrences matches {0..1} matches {    -- Autonomic signs
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0061] occurrences matches {0..1} matches {    -- Nasal stuffiness
												value matches {
													0|[local::at0055],
													1|[local::at0056]
												}
											}
											ELEMENT[at0070] occurrences matches {0..1} matches {    -- Respiratory rate
												value matches {
													0|[local::at0046],
													1|[local::at0072]
												}
											}
										}
									}
									CLUSTER[at0044] occurrences matches {0..1} matches {    -- Gastrointestinal signs
										items cardinality matches {1..*; unordered} matches {
											ELEMENT[at0076] occurrences matches {0..1} matches {    -- Excessive sucking
												value matches {
													0|[local::at0055],
													1|[local::at0056]
												}
											}
											ELEMENT[at0079] occurrences matches {0..1} matches {    -- Poor feeding
												value matches {
													0|[local::at0055],
													2|[local::at0056]
												}
											}
											ELEMENT[at0082] occurrences matches {0..1} matches {    -- Feed tolerance
												value matches {
													0|[local::at0055],
													1|[local::at0084]
												}
											}
											ELEMENT[at0085] occurrences matches {0..1} matches {    -- Stools
												value matches {
													0|[local::at0046],
													2|[local::at0087]
												}
											}
										}
									}
									ELEMENT[at0088] occurrences matches {0..1} matches {    -- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..20|}
											}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0049] matches {*}    -- Item tree
		}
	}

ontology
	terminologies_available = <"SNOMED-CT", ...>
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Simplified Finnegan Neonatal Abstinence Score">
					description = <"An assessment score used to evaluate the severity of opioid withdrawal in newborns every 3-4 hours.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0039"] = <
					text = <"Central nervous system signs">
					description = <"">
				>
				["at0042"] = <
					text = <"Autonomic signs">
					description = <"">
				>
				["at0044"] = <
					text = <"Gastrointestinal signs">
					description = <"">
				>
				["at0045"] = <
					text = <"Cry">
					description = <"">
				>
				["at0046"] = <
					text = <"Normal">
					description = <"*">
				>
				["at0047"] = <
					text = <"Excessive or continuous">
					description = <"High-pitched, inconsolable, and uncontrollable crying that is excessive or continuous">
				>
				["at0049"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0050"] = <
					text = <"Tremors">
					description = <"">
				>
				["at0051"] = <
					text = <"None">
					description = <"">
				>
				["at0052"] = <
					text = <"Tremors disturbed (mild, moderate, or severe)">
					description = <"Tremors when disturbed or not asleep">
				>
				["at0053"] = <
					text = <"Tremors undisturbed (mild, moderate, or severe)">
					description = <"Tremors when undisturbed or asleep">
				>
				["at0054"] = <
					text = <"Increased muscle tone">
					description = <"">
				>
				["at0055"] = <
					text = <"No">
					description = <"">
				>
				["at0056"] = <
					text = <"Yes">
					description = <"An increase in muscle tone">
				>
				["at0059"] = <
					text = <"< 2 or 3 hours">
					description = <"Sleeping for less than three hours after feeding.">
				>
				["at0060"] = <
					text = <"< 1 hour">
					description = <"Sleeping for less than one hour after feeding.">
				>
				["at0057"] = <
					text = <"Sleep">
					description = <"">
				>
				["at0061"] = <
					text = <"Nasal stuffiness">
					description = <"">
				>
				["at0070"] = <
					text = <"Respiratory rate">
					description = <"">
				>
				["at0072"] = <
					text = <"> 60/min">
					description = <"Respiratory rate is less than 60/min with or without retraction">
				>
				["at0076"] = <
					text = <"Excessive sucking">
					description = <"">
				>
				["at0079"] = <
					text = <"Poor feeding">
					description = <"">
				>
				["at0082"] = <
					text = <"Feed tolerance">
					description = <"">
				>
				["at0084"] = <
					text = <"Regurgitation or projectile vomiting">
					description = <"">
				>
				["at0085"] = <
					text = <"Stools">
					description = <"">
				>
				["at0087"] = <
					text = <"Loose or watery stools">
					description = <"">
				>
				["at0088"] = <
					text = <"Total score">
					description = <"To give the total score of the Simplified Finnegan Neonatal Abstinence Score (sFNAS).">
				>
			>
		>
		["es-ar"] = <
			items = <
				["at0000"] = <
					text = <"Evaluación simplificada del puntaje de Finnegan de Abstinencia Neonatal">
					description = <"Evaluación de riesgo simplificada para evaluar la gravedad de la abstinencia de opioides en recién nacidos cada 3 a 4 horas.">
				>
				["at0001"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Cualquier evento">
					description = <"">
				>
				["at0003"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0039"] = <
					text = <"Signos del Sistema Nervioso Central">
					description = <"">
				>
				["at0042"] = <
					text = <"Signos autonomicos">
					description = <"">
				>
				["at0044"] = <
					text = <"Signos gastroinstentinales">
					description = <"">
				>
				["at0045"] = <
					text = <"Llanto">
					description = <"">
				>
				["at0046"] = <
					text = <"Normal">
					description = <"">
				>
				["at0047"] = <
					text = <"Excesivo o continuo">
					description = <"Llanto agudo, inconsolable e incontrolable, excesivo o continuo.">
				>
				["at0049"] = <
					text = <"Item tree">
					description = <"@ internal @">
				>
				["at0050"] = <
					text = <"Temblores">
					description = <"">
				>
				["at0051"] = <
					text = <"Ninguno">
					description = <"">
				>
				["at0052"] = <
					text = <"Temblores perturbados (leves, moderados o severos)">
					description = <"Temblores cuando está siendo perturbado o al estar despierto.">
				>
				["at0053"] = <
					text = <"Temblores no perturbados (leves, moderados o severos)">
					description = <"Temblores cuando no está siendo perturbado o al dormir.">
				>
				["at0054"] = <
					text = <"Tono muscular incrementado">
					description = <"">
				>
				["at0055"] = <
					text = <"No">
					description = <"">
				>
				["at0056"] = <
					text = <"Sí">
					description = <"">
				>
				["at0059"] = <
					text = <"< 2 o 3 horas">
					description = <"Dormir por menos de 3 horas después de ser alimentado.">
				>
				["at0060"] = <
					text = <"< 1 hora">
					description = <"Dormir por menos de 1 horas después de ser alimentado.">
				>
				["at0057"] = <
					text = <"Sueño">
					description = <"">
				>
				["at0061"] = <
					text = <"Congestión nasal">
					description = <"">
				>
				["at0070"] = <
					text = <"Frecuencia respiratoria">
					description = <"">
				>
				["at0072"] = <
					text = <"> 60/min (en)">
					description = <"Frecuencia respitatoria menor a 60 por minuto o sin retracción.">
				>
				["at0076"] = <
					text = <"Succión excesiva">
					description = <"">
				>
				["at0079"] = <
					text = <"Alimentación deficiente">
					description = <"">
				>
				["at0082"] = <
					text = <"Tolerancia a la alimentación">
					description = <"">
				>
				["at0084"] = <
					text = <"Regusgitación o vómito en proyectil">
					description = <"">
				>
				["at0085"] = <
					text = <"Heces">
					description = <"">
				>
				["at0087"] = <
					text = <"Heces blandas o acuosas">
					description = <"">
				>
				["at0088"] = <
					text = <"Puntaje total">
					description = <"Calcular el puntaje total de la Evaluación simplificada del puntaje de Finnegan de Abstinencia Neonatal (sFNAS por sus siglas en inglés).">
				>
			>
		>
	>
	term_binding = <
		["SNOMED-CT"] = <
			items = <
				["at0047"] = <[SNOMED-CT(2022-02-28)::95629002]>
				["at0045"] = <[SNOMED-CT(2022-02-28)::28263002]>
				["at0057"] = <[SNOMED-CT(2022-02-28)::258158006]>
				["at0052"] = <[SNOMED-CT(2022-02-28)::26079004]>
				["at0053"] = <[SNOMED-CT(2022-02-28)::26079004]>
				["at0070"] = <[SNOMED-CT(2022-02-28)::86290005]>
				["at0072"] = <[SNOMED-CT(2022-02-28)::271823003]>
				["at0076"] = <[SNOMED-CT(2022-02-28)::90485000]>
				["at0079"] = <[SNOMED-CT(2022-02-28)::129007004]>
			>
		>
	>
